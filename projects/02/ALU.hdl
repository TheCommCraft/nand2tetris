// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    /*
    if (zx == 1) {
        zeroedx = 0;
    } else {
        zeroedx = x;
    }
    */
    Mux16(a=x, sel=zx, out=zeroedx);
    Mux16(a=y, sel=zy, out=zeroedy);

    /*
    if (nx == 1) {
        finalx = !zeroedx;
    } else {
        finalx = zeroedx;
    }
    */
    Not16(in=zeroedx, out=nzeroedx);
    Not16(in=zeroedy, out=nzeroedy); 

    Mux16(a=zeroedx, b=nzeroedx, sel=nx, out=finalx);
    Mux16(a=zeroedy, b=nzeroedy, sel=ny, out=finaly);

    /*
    if (f == 1) {
        result = finalx + finalx;
    } else {
        result = finalx & finaly;
    }
    */
    Add16(a=finalx, b=finaly, out=addout);
    And16(a=finalx, b=finaly, out=andout);
    
    Mux16(a=andout, b=addout, sel=f, out=result);
    
    /*
    if (no == 1) {
        out = !result;
    } else {
        out = result;
    }
    */
    Not16(in=result, out=nresult);

    /*
    if (out[15] == 1) {
        ng = 1;
    } else {
        ng = 0;
    }
    */
    Mux16(
        a=result, b=nresult, sel=no,
        out=out,
        out[15]=ng, // Evil bit magic
        out[0..7]=o8a,
        out[8..15]=o8b
    );

    /*
    if (out == 0) {
        zr = 1;
    } else {
        zr = 0;
    }
    */
    Or8Way(in=o8a, out=ora);
    Or8Way(in=o8b, out=orb);
    Or(a=ora, b=ora, out=nzr);
    Not(in=nzr, out=zr);
}