// 16 tile rows
// 32 tile columns
// 256 pixel rows
// 512 pixel columns
class Board {
    field Array data;

    // function Array makeArray() {
    //     return Array.new(Board.getWidth() * Board.getHeight());
    // }

    function Board new(MemArena arena) {
        var Board t;
        var Array data;
        var int i;
        let t = arena.getAt(255);
        let data = arena.getAt(256);
        let i = 0;
        while (i < Board.getSize()) {
            let data[i] = 0;
            let i = i + 1;
        }
        let t[0] = data;
        return t;
    }

    method void dispose() {
        do data.dispose();
        do Game.disposeSafely(this);
        return;
    }

    method Array getRawData() {
        return data;
    }

    function int getWidth() {
        return 32;
    }

    function int getHeight() {
        return 16;
    }

    function int getSize() {
        return Board.getWidth() * Board.getHeight();
    }

    function int getIndexAtPos(int x, int y) {
        return x + (y * Board.getWidth());
    }

    method void setAtPos(int x, int y, int value) {
        if ((x < 0) | (y < 0) | (x > (Board.getWidth() - 1)) | (y > (Board.getHeight() - 1))) {
            return;
        }
        let data[Board.getIndexAtPos(x, y)] = value;
        if (value = 0) {
            do Screen.setColor(false);
        } else {
            do Screen.setColor(true);
        }
        do Screen.drawRectangle(x * 16, y * 16, x * 16 + 15, y * 16 + 15);
        return;
    }

    method int getAtPos(int x, int y) {
        if ((x < 0) | (y < 0) | (x > (Board.getWidth() - 1)) | (y > (Board.getHeight() - 1))) {
            return 0;
        }
        return data[Board.getIndexAtPos(x, y)];
    }
}